#!/usr/bin/perl
#
# Compile and run random LilyPond snippets specified on command line:
#
# ly-fu c des ees c des bes c aes

use strict;
use warnings;

use File::Basename qw/basename/;
use File::Spec ();
use File::Temp qw/tempfile/;
use Getopt::Long qw/GetOptions/;
use IO::Handle;

my $BASENAME         = basename($0);
my @LILYPOND         = qw/lilypond/;
my $LILYPOND_VERSION = '2.12.0';
my @MIDI_PLAYER      = qw/timidity/;
my $MIDI_SUFFIX      = '.midi';
my @CLEANUP_SUFFIX   = qw/ps pdf/;

my $tempo       = 130;
my $instrument  = "Piano";
my $relative    = q{c'};
my $partial     = q{};
my $save_layout = 0;
my $skip_midi   = 0;
my ( $is_absolute, $repeats );
my $prestaff = q{};

########################################################################
#
# MAIN

unless (@ARGV) {
  warn "Usage: $BASENAME [note1 note2 ..|-]\n";
  exit 64;
}

GetOptions(
  "absolute|abs|a" => \$is_absolute,
  "instrument|i=s" => \$instrument,
  "layout|l"       => \$save_layout,
  "partial|p=s"    => \$partial,
  "relative|b=s"   => \$relative,
  "repeats|r=s"    => \$repeats,
  "silent|s"       => \$skip_midi,
  "tempo|t=s"      => \$tempo,
);

push @LILYPOND, '--pdf' if $save_layout;

my ( $src_fh, $src_fname ) = tempfile(
  "$BASENAME.XXXXXXX",
  DIR    => File::Spec->tmpdir,
  UNLINK => 0
);
my $midi_fname = $src_fname . $MIDI_SUFFIX;

if ( @ARGV == 1 and $ARGV[0] eq '-' ) {
  chomp( @ARGV = <STDIN> );
}

if ( defined $repeats ) {
  @ARGV = (@ARGV) x $repeats;
}

$prestaff = "\\relative $relative" unless $is_absolute;

my $ly_template = <<"END_TMPL";
\\version "$LILYPOND_VERSION"
\\score {
  \\new Staff << $prestaff {
    \\set Staff.midiInstrument = #"$instrument"
    \\tempo 4=$tempo
    $partial
    @ARGV
  } >>
  \\midi { }
  \\layout { }
}
END_TMPL

$src_fh->autoflush(1);
print $src_fh $ly_template;

my $exit_status = 0;

# NOTE Lilypond will add suffix to output, hopefully the same suffix
# used by this script (glob for it if it can be dynamic).
system( @LILYPOND, "--output=$src_fname", $src_fname ) == 0
  or die "error: lilypond compile failed: $?\n";
if ( !$skip_midi ) {
  system( @MIDI_PLAYER, $midi_fname );
  $exit_status = $? >> 8;
}

unlink $midi_fname;
if ( !$save_layout ) {
  unlink $src_fname;
  unlink "$src_fname.$_" for @CLEANUP_SUFFIX;
}
exit $exit_status;
