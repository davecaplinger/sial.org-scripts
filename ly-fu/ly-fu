#!/usr/bin/perl
#
# Compile and run random LilyPond snippets specified on command line:
#
# ly-fu c des ees c des bes c aes

use strict;
use warnings;

use File::Basename qw/basename/;
use File::Spec ();
use File::Temp qw/tempfile/;
use Getopt::Long qw/GetOptions/;
use IO::Handle;

my $BASENAME = basename($0);

my @LILYPOND         = qw/lilypond/;
my $LILYPOND_VERSION = '2.12.0';

my @MIDI_PLAYER  = qw/timidity/;
my $MIDI_SUFFIX  = '.midi';
my @SCORE_READER = qw/open -a Preview.app/;
my $SCORE_SUFFIX = '.pdf';

my @CLEANUP_SUFFIX = qw/ps pdf/;

my $tempo       = 130;
my $instrument  = "Piano";
my $do_open     = 0;
my $relative    = q{c'};
my $partial     = q{};
my $save_layout = 0;
my $skip_midi   = 0;
my ( $is_absolute, $repeats );
my $prestaff    = q{};
my $sleep_kluge = 0;

########################################################################
#
# MAIN

print_help() unless @ARGV;

if ( exists $ENV{'MIDI_EDITOR'} ) {
  @MIDI_PLAYER = map { s{(?<!\\)\\}{}g; $_ } split /(?<!\\)\s+/,
    $ENV{'MIDI_EDITOR'};
}

GetOptions(
  "absolute|abs|a" => \$is_absolute,
  'help|h|?'       => \&print_help,
  "instrument|i=s" => \$instrument,
  "layout|l"       => \$save_layout,
  "open"           => \$do_open,
  "partial|p=s"    => \$partial,
  "relative|b=s"   => \$relative,
  "repeats|r=s"    => \$repeats,
  "silent|s"       => \$skip_midi,
  "sleep|S=s"      => \$sleep_kluge,
  "tempo|t=s"      => \$tempo,
);

$save_layout = 1 if $do_open;
push @LILYPOND, '--pdf' if $save_layout;

my ( $src_fh, $src_fname ) = tempfile(
  "$BASENAME.XXXXXXX",
  DIR    => File::Spec->tmpdir,
  UNLINK => 0
);
my $midi_fname  = $src_fname . $MIDI_SUFFIX;
my $score_fname = $src_fname . $SCORE_SUFFIX;

if ( @ARGV == 1 and $ARGV[0] eq '-' ) {
  chomp( @ARGV = <STDIN> );
}

if ( defined $repeats ) {
  @ARGV = (@ARGV) x $repeats;
}

$prestaff = "\\relative $relative" unless $is_absolute;

my $ly_template = <<"END_TMPL";
\\version "$LILYPOND_VERSION"
\\score {
  \\new Staff << $prestaff {
    \\set Staff.midiInstrument = #"$instrument"
    \\tempo 4=$tempo
    $partial
    @ARGV
  } >>
  \\midi { }
  \\layout { }
}
END_TMPL

$src_fh->autoflush(1);
print $src_fh $ly_template;

my $exit_status = 0;

# NOTE Lilypond will add suffix to output, hopefully the same suffix
# used by this script (glob for it if it can be dynamic).
system( @LILYPOND, "--output=$src_fname", $src_fname ) == 0
  or die "error: lilypond compile failed: $?\n";
if ( !$skip_midi ) {
  unless ( system( @MIDI_PLAYER, $midi_fname ) ) {
    warn "warning: '@MIDI_PLAYER $midi_fname' failed: $?\n";
    $exit_status = 70;
  }
}

system( @SCORE_READER, $score_fname ) if $do_open;

if ( !$save_layout ) {
  # Finale.app can blow up if file unlinked from underneath it? So need
  # this delay, or save the layout option set.
  sleep int $sleep_kluge if !$save_layout and $sleep_kluge > 0;

  unlink $midi_fname;
  unlink $src_fname;
  unlink "$src_fname.$_" for @CLEANUP_SUFFIX;
}

exit $exit_status;

########################################################################
#
# SUBROUTINES

sub print_help {
  warn <<"END_HELP";
Usage: $0 [options] "lilypond input" "more input" ...

The lilypond input will likely need to be quoted as lilypond input may
clash with various shell metacharacters.

  --absolute      Notation assumed to be absolute.
  --instrument    Set MIDI instrument (see lilypond docs).
  --partial|p     Lilypond fragment played once at beginning.
  --relative|b    Specify what not input is relative to.
  --repeats|r     How many times to repeat the lilypond input.
  --tempo|t       Set a tempo for the music.

  --layout|l      Save the MIDI and other various files.
  --open          Show the score in some sort of viewer.
  --silent|s      Skip generating MIDI.
  --sleep|S       Sleep before unlink of various tmp files.

END_HELP
  exit 64;
}
